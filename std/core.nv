module core

struct Box(A) {
    value: $A
}

fn extends show(self: Box($A)) {
    println(Cast::string(self.value))
}



fn Gen(start: Int) -> fn() -> Int {
    let i = Box(start)
    return fn() -> Int {
        i.value += 1
        return i.value - 1
    }
}

// built in Option type 

fn extends orError(self: Option($A), msg: String) -> $A {
    if self.isSome() {
        return self.unwrap()
    }
    println(msg)
    exit()
    return unreachable() @[T: $A]
}

fn extends orDefault(self: Option($A), default: $A) -> $A {
    if self.isSome() {
        return self.unwrap()
    }
    return default
}

fn extends orDoFn(self: Option($A), f: fn() -> $A) -> $A {
    if self.isSome() {
        return self.unwrap()
    }
    return f()
}

fn extends isNone(self: Option($A)) -> Bool {
    return !(self.isSome())
}

// Maybe type

enum Maybe(A) {
    Just: $A,
    Nothing
}

fn extends toMaybe(self: Option($A)) -> Maybe($A) {
    if self.isSome() {
        return Maybe::Just(self.unwrap())
    }
    return Maybe::Nothing() @[A: $A]
}

fn extends isJust(self: Maybe($A)) -> Bool {
    match self {
        Just(x) -> {return true}
        Nothing() -> {return false}
    }
    return false
}

// result type

enum Result(A, B) {
    Ok: $A,
    Err: $B
}

fn extends toResult(self: Option($A), err: $B) -> Result($A, $B) {
    if self.isSome() {
        return Result::Ok(self.unwrap())
    }
    return Result::Err(err)
}

// basic list functions

fn extends iota(n: Int) -> [Int] {
    let result = []: Int
    for i <- 0; i < n; i += 1 {
        result.push(i)
    }
    return result
}

fn extends toStep(start: Int, end: Int, step: Int) -> [Int] {
    let result = []: Int
    for i <- start; i < end; i += step {
        result.push(i)
    }
    return result
}

fn extends to(self: Int, end: Int) -> [Int] {
    let result = []: Int
    for i <- self; i < end; i += 1 {
        result.push(i)
    }
    return result
}

// Core functions // like pythons basic functions

fn range(start: Int, end: Int) -> [Int] {
    let result = []: Int
    for i <- start; i < end; i += 1 {
        result.push(i)
    }
    return result
}

fn range(end: Int) -> [Int] {
    return range(0, end)
}

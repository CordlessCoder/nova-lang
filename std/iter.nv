module iter

import core

struct Iter(A) {
    next: () -> ?$A
}

fn extends(Iter) fromVec(input: [$A]) -> Iter($A) {
    let index = Box(0)  
    return Iter(fn() -> ?$A {
        if index.value >= input.len() {
            return ?$A
        } else {
            let result = Some(input[index.value]);
            index.value += 1
            return result
        }
    })
}

fn extends(Iter) enumerate(it: Iter($A)) -> Iter(#(Int,$A)) {
    let index = Box(0)  
    return Iter(fn() -> ?#(Int,$A) {
        let t = it::next()
        if t.isSome() {
            let result = #(index.value, t.unwrap())
            index.value += 1
            return Some(result)
        } else {
            return ?#(Int,$A)
        }
    })
}

fn extends(Iter) fromFn(input: () -> ?$A) -> Iter($A) {
    return Iter(input)
}

fn extends filter(it: Iter($A), f:($A) -> Bool) -> Iter($A) {
    return Iter(fn() -> ?$A {
        let t = it::next()
        while t.isSome() {
            if f(t.unwrap()) {
                return t
            }
            t = it::next() 
        }
        return ?$A
    })
}

fn extends map(it: Iter($A), f:($A) -> $B) -> Iter($B) {
    return Iter(fn() -> ?$B {
        let t = it::next() 
        if t.isSome() {
            return Some(f(t.unwrap()))
        } else {
            return ?$B
        }
    })
}

fn extends show(it: Iter($A)) {
    let t: ?$A = it::next() 
    while t.isSome() {
        println(t.unwrap())
        t = it::next() 
    }
}

fn extends collect(it: Iter($A)) -> [$A] {
    let t: ?$A = it::next() 
    let list = []:$A
    while t.isSome() {
        list.push(t.unwrap())
        t = it::next() 
    }
    return list
}